!% -SD

!============================================================================
! In-progress work by Ben Kidwell ("mycroftiv")
! Currently all rights reserved, I'll put a more open license on it when it's done
! Thanks to Inform Beginner's Guide Captain Fate for the template

Constant Story "Harmonic Time-Bind Ritual Symphony";
Constant Headline
"^9:15 PM 27 Sept.  2015
^
^You type the introductory text of the game into the acme text editor.
It is running on a Plan 9 cpu/file server that you connect to with
Drawterm.  You have spent the past thirty minutes on the porch with
Staci, watching the lunar eclipse.  Your room is filled with cheap
computers running customized grid software called Advanced Namespace
Tools.
^
^There is a mostly empty cup of coffee here.
^
^There is a Moon Man beer here, half full.
^
^Outside, the moon is completely eclipsed, a shadowy phantom of its
usual brilliance.
^
^>go outside
^
^You walk onto the porch.  You contemplate the mysterious beauty of the
moon.  Around your neck, the Lunar Revolution Power Pendant resonates
with the hum of the night; a slight breeze, crickets.  The bridge to
the west has been closed for weeks and everything has been quiet and
still.
^
^It has been two and a half years of lunacy since New Year's 2013, when
you made the resolution to return to Plan 9 software development.  You
weren't trying start a revolution; you just wanted something useful to
do with your time.  The VALISpectrum colors, the singing hippie
housekeepers, the dock by the castle, riding Lunar Lightning to Cosmic
Delights and visiting the Afterlife, falling in love - everything
ensued from making the binds of fiction and reality that transformed
you into

^
^THE MOON COMPUTER (Movement 1)
^March 11 2013
^
^";

Release 0;  Serial "151014";    ! for keeping track of public releases

Include "parser";

Object  LibraryMessages         ! must be defined between Parser and VerbLib
  with  before [;
          Buy:    "You think about money and dream of transforming the economic system with Plan 9 unique digital object alternative community currencies.";
          Dig:    "In another reality, your dog-self burrows deeply within the Earth, uncovering exactly the items needed to continue your journey. In this world, you accomplish nothing.";
          Pray:   "You offer prayers to many Gods as well as the laws of physics.";
          Sing:   "You start with a bit of Mozart's Marriage of Figaro, move on to a few of your favorite Wu-Tang verses, and conclude with a heartfelt imitation of how IU would sound singing Ripple by the Grateful Dead.";
          Sleep:  "You drift for awhile into another realm, a tourist in one of the uncountable branches of the multiverse.";
          Sorry:  "Rather than being caught up in apologies and regrets, you try to maintain focus on future improvement.";
          Strong: "Say that again, and louder! Shout it! Let the world know how you feel!";
          Swim:   "The Madison lakes are beautiful to look at, and some people have the immune system required to cope with algae and bacteria, but you prefer to sit on the shore and watch the chromatic flickering of the ripples.";
          Miscellany:
            if (lm_n == 38)
                "That action is outside the scope of this simulation.";
            if (lm_n == 39)
                "This game world does not provide that level of detail.";
        ];

Include "verblib";

!============================================================================
! Object classes

Class   Room
  with  description "UNIMPLEMENTED ROOM DESCRIPTION",
  has   light;

Class   Decor
  with  description "UNIMPLEMENTED DECOR DESCRIPTION",
  has   scenery;

!============================================================================
! The game locations

Room    milw_street "Outside my house"
	with	name 'Milwaukee_St.',
			description "Your section of Milwaukee Street is directly across from a small farm which remains as an agricultural island within the city. An open field and a barn are across the street. Toward the southwest is Atwood Street leading toward downtown. Monona Drive is to the south. The edge of town lies toward the northeast.",
		before [; enter: PlayerTo(home); return true;],
		in_to home,
		sw_to atwood_av,
		s_to mon_cot,
		ne_to etown,
		cant_go "You go for a brief walk in the neighborhood, then return home. You may travel sw, s, ne, or enter your house.";

Room	home "Inside my house"
	with	name 'home' 'residence' 'house',
			description "This is a modest one-story house, cluttered by a decade of being lived-in. Your Plan 9 terminal is here, connected to the grid servers downstairs.",
		out_to milw_street,
		d_to basement;

Room	basement "My house, in the basement"
	with	name 'basement' 'downstairs',
			description "Your ex-girlfriend used to stay down here, but now the area is mostly storage...and your grid hardware. In the center of the dark concrete floor is a flimsy table with numerous aging computers both under it and on top. A tangle of ethernet and power cables criss-cross the room like spiderwebs.",
		u_to home;

Room	atwood_av "Atwood Avenue, by several shops"
	with	description "Atwood Avenue is one of the streets that defines the character of the near east side. The feel is funky and freak-friendly. Next to you is a cluster of intriguing stores: Cosmic Delights, Revolution Cycles, and Absolutely Art. Willy street is to the west, your home is northeast, and Atwood becomes Monona Drive to the southeast.",
		ne_to milw_street,
		se_to mon_cot,
		w_to willy_east;

Room	willy_east "Willy Street, east end"
	with	description "The east end of Williamson street is vibrant with a wide assortment stores and an eclectic mix of local pedestrians and potential time travelers. Traffic is thick around the Willy St. grocery co-op, a neighborhood institution.",
		s_to willy_coop,
		in_to willy_coop,
		e_to atwood_av,
		w_to willy_west;

Room	willy_coop "Willy St. Grocery Co-op"
	with	description "The Willy street co-op has been selling organic foods and Dr. Bronner's magic soaps to the local crunchies for decades.",
		out_to willy_east,
		n_to willy_east;

Room	willy_west "Willy Street, west end"
	with	description "The west end of Williamson street is where the east side of Madison ends and the downtown area begins. A tangle of memories from different decades of your life is woven into the landscape of the city here. Mother Fool's coffeeshop is on the south side of the street, radiating bohemian charm acquired from decades of service to local culture. John Nolen drives curves around the lake to the west, and the Capitol Square is northwest.",
		s_to mother_fools,
		in_to mother_fools,
		e_to willy_east,
		w_to john_nolen,
		nw_to cap_square;

Room	mother_fools "Mother Fool's Coffeeshop"
	with	description "Small, funky, and redolent of the blood sweat and tears of artists trying to transform caffeine into inspiration.",
		n_to willy_west,
		out_to willy_west;

Room	john_nolen "John Nolen Drive"
	with	description "Adjacent to lake Monona, John Nolen Drive is a wide multi-lane road which passes underneath the curved contours of the Monona Terrace convention center. The view from across the lake looking toward downtown is Madison's skyline-postcard view. A heavily used bike path teems with cyclists and joggers. The Capitol square is north, State Street is to the northwest, and the Miffyland residential neighborwood is west.",
		n_to cap_square,
		nw_to state_street,
		w_to miffyland;

Room	cap_square "Capitol Square"
	with	description "On a hill in the center of downtown, the Capitol building is a smaller imitation of the federal Capitol building in Washington D.C. The street grid converges to a perfect square around it, and it acts as the 0,0 origin of the street numbering system. Various rumors and legends are passed around the local countercultural community - it is claimed to be built on top of an ancient sacred spring. You remember being a teenager and carrying out the ritual of ingesting psychedelics and using your rights as a citizen to enter the building and lay flat in the center of the marble floor, staring upwards into the dome and watching the mural melt and transform. Notable streets and landmarks lie south, west, north, and southeast.",
		se_to willy_west,
		s_to john_nolen,
		w_to state_street,
		n_to james_madison;

Room	state_street "State Street"
	with	description "The downtown and campus are focused on this pedestrian-only open air shopping mall. Dozens of shops and throngs of pedestrians create a big-city feel for a few blocks. The proximity of the large state university campus means the crowd is quite young, and weekend nights are often a mix of the charming antics of local eccentrics with less charming rowdy sophomoric intoxication. The Miffyland neighborhood is south, the Capitol is east.",
		s_to miffyland,
		e_to cap_square;

Room	miffyland "Miffyland, the student ghetto"
	with	description "This small residential neighborhood is a mix of old quasi-victorian houses subdivided into apartments, and more recent larger apartment buildings. Many university students move here after their time in the dorms, and on summer nights the porches are full of young people taking a break from their studies with the traditional libations of beer and weed. You lived here yourself about fifteen years ago. State Street is north, and John Nolen Drive is east.",
		e_to john_nolen,
		n_to state_street;
		
Room	james_madison "James Madison Park, Mendota lakeside"
	with	description "A wide park on the shore of Lake Mendota, this is often a perfect place. Gulls circle on wind currents above the waves that break against the shore. A frisbee floats back and forth between a pair of baseball caps. During summer, sunbathers appear like flowers on the grass. The Capitol Square is to the south.",
		s_to cap_square;

Room	mon_cot "Monona Drive and Cottage Grove Road"
	with	description "Traffic is heavy at the intersection of these roads. Nearby is Java Cat Cafe. Monona Drive continues to the south, Atwood Avenue is northwest, and your home is to the north.",
		in_to java_cat,
		e_to java_cat,
		n_to milw_street,
		nw_to atwood_av,
		s_to mon_mid;

Room	java_cat "Java Cat Cafe"
	with	description "Your local cafe has been your comfort for the past several years. Days and sometimes whole weeks would go by where the only people you saw were the baristas here. You always felt accepted here, even when your disheveled appearance and stumbling arthritic gait made you feel awkward in general society. Light streams in through the tall glass windows at the front of the cafe. A display case of homemade gelato flavors is by the door. Numerous cat-themed decorations adorn the walls and counters.",
		out_to mon_cot,
		w_to mon_cot;

Room	mon_mid "Monona Drive, by a weathered strip-mall"
	with	description "Monona drive becomes more suburban as it heads south, but there is still character in the businesses here. A neighborhood pizzeria has recently moved into this location on the west side of the street.",
		in_to rossis_pizza,
		w_to rossis_pizza,
		n_to mon_cot;

Room	rossis_pizza "Rossi's Pizza and Vintage Arcade"
	with	description "Memorabilia from classic 50s, 60s, 70s, and 80s movies and tv shows covers the walls of this neighborhood pizzeria. The smell of spicy-sweet tomato sauce and brown-crisp pizza crust fills the air. A lava lamp sits on one of the tables. From the back room, you hear the alluring sound of the 8-bit sound effects of your youth.",
		out_to mon_mid,
		e_to mon_mid,
		in_to rossis_arcade,
		n_to rossis_arcade;

Room	rossis_arcade "Rossi's Pizza, in the Vintage Arcade room"
	with	description "A time-warped paradise. Vintage arcade cabinets surround you. The CRT displays glow with color-bleed, the sound effects are a symphonic rhapsody of buzzing lasers, and each machine is a digital poem tattooed on your heart during the 1980s. Back then, you never had enough quarters...but now, you finally do.",
		out_to rossis_pizza,
		s_to rossis_pizza;

Room	etown "Northeast edge of town"
	with	description "This is the northeast edge of town, by the shopping mall and the interstate. It is mostly lacking in the distinctive charms of the rest of the city. The usual reason for being out here is to visit the 24-hour restaurant to the north, whose sign faces the highway, glowing like a green beacon.",
		in_to eside_hq,
		n_to eside_hq,
		sw_to milw_street;

Room	eside_hq "24-hour family restaurant"
	with	description "This national chain restaurant serves edible food around the clock. During the graveyard shift, the waitstaff are often friendly and interesting people who are bored enough to be curious about whatever conversation you make. There are a few other patrons, a mix of business travelers staying at the nearby hotels, late-night partiers out after the bar, and random eccentrics such as yourself.",
		out_to etown,
		s_to etown;

!============================================================================
! The room decorations

Decor	farm	"farm" milw_street
	with 	name 'farm' 'barn' 'corn' 'agriculture' 'field',
			description "The farm across the street is one of the main reasons you chose to move into this house ten years ago. The way property development goes in Madison, it seems like a miracle that the farmer has held onto this land in the midst of the city. The barn is run-down and decrepit and the field is not yet planted for the season.";

Decor	house	"house" milw_street
	with	name 'house' 'home' 'dwelling',
			description "Your house is on the corner of the block. It is painted a light yellow and has a fenced in side yard. You have lived here for the past decade.";

Decor	terminal "terminal" home
	with	name 'terminal' 'computer' 'plan' 'plan9' 'grid' '9grid' 'system',
			description "Your terminal glows with a Mondrian-like arrangement of pastel windows, each rio bound into a different namespace. Multiple rc shells are running, acme is open, and several irc channels are available within hubfs.";

Decor	grid "grid" basement
	with	name 'grid' 'table' 'computer' 'computers' 'cables' 'ethernet' 'webs' 'spiderwebs' 'power' 'desk' '9grid',
			description "The physical hardware of your grid is cheap and worn. A mixture of no-name hardware and old dell minitowers, the processor speed, RAM, and storage space is all well below the current standard. The software is a different story - your Advanced Namespace Tools weave the physical boxes together into a coordinated whole. Sometimes you feel like the data within, how it is organized and flows, is like a living thing.";

!============================================================================
! The takable objects

object	wallet "wallet" home
	with	name 'wallet' 'money',
			description "An old black leather wallet containing the standard mix of cash, credit cards, and random scraps of paper.";


!============================================================================
! Entry point routines

[ Initialise;
    location = home;
    lookmode = 2;
    "^^The full moon amidst clouds spills milky brightness everywhere.
Summer is just barely over but the air is still warm and moist. Staci
is standing on tip-toe, looking over the edge of the rooftops and
treebranches. Oh! I can tell it's started, she says. You stand up
and walk across the porch, slipping your arm around her as you turn to
watch the sky. What had been a low full circle wreathed by clouds is
higher now and shinining clearly, but no longer full - darkness was
creeping across the surface as the Moon swung into the path of the
shadow cast by Earth.  You peer upwards together, savoring the moment.
The night air is sweet and warm, you are with someone you love...and
the Castle is about to reopen.^^";

];

!============================================================================
! Other functions

[ Aboutsub;
	"^Harmonic Time-Bind Ritual Symphony is a game based on my actual lived experience of spring and summer 2013. During that time, I was able to experience my life as a work of interactive fiction created by a brilliant author. I felt that the nature of the quantum multiverse meant that it was factually true that I was a character in novels and songs and videogames in other universes, and I played my role to the hilt. I felt that my manic madness was a gift which I could use for artistic creativity, and that my racing mind was fully up to solving the puzzles that the designer of my reality had laid out for me. It was the most glorious experience imaginable, and by the middle of the summer I felt like I was becoming a kind of Ultima IV-style Avatar of multiversal artistic consciousness and with just a little more spiritual insight and creative freedom, I'd literally be able to unlock the Vishnu-path Extra Arms upgrade for my character, which I might need in case the tentacle in the lake that was a whisker of Cthulhu's Moustache attacked.^
	^This game was created to share the wild, unbounded joy I felt for those months. Thanks to everyone who was a part of my life during that time, and thank you for playing this work of Interactive Fiction.^";
];

[ Bindsub;
	"^You make the bind of ", (name) noun, " on top of ", (name) second, " and the transformation begins...^";
];

[ Creditssub;
	"^Written and programmed by Ben Kidwell (mycroftiv)
	 ^Co-written and edited and tested by Staci Straw
	 ^Created in Plan 9 using Inform 6, initial template from Inform Beginner's Guide^";
];

[ Helpsub;
	"If you are new to Interactive Fiction, the basic commands are moving around using compass directions (n s e w ne nw se sw up down in out), examining things (x NAME), taking things and looking in your inventory (take NAME, i), and talking to characters (talk to NAME). Please check an internet guide such as this guide by Emily Short:
^http://inform7.com/learn/eg/dm/IntroductionToIF.pdf 
^to learn the basics. This game has a few unusual verbs and gameplay possibilities, such as the bind command from the Plan 9 operating system. Specific hints are available if you type hint NAME for hints relevant to an object or location.^";
];

[ Hintsub;
	"^Once I make puzzles there will be hints.";
];	

!============================================================================
! Standard and extended grammar

Include "grammar";

!============================================================================

Verb "about" *	-> About;
Verb "bind"	*	noun	noun	-> Bind;
Verb "credits" *	-> Credits;
Verb "help" *	-> Help;
Verb "hint" * noun	-> Hint;
